plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.gradleup.shadow' version '8.3.6'
}

ext {
    javafxVersion = '21.0.5'
    javafxModules = ['base', 'controls', 'fxml', 'graphics']
    platforms = ['win', 'linux', 'mac']
}

group = 'avs.simulation'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    javafxModules.each { module ->
        platforms.each { platform ->
            implementation "org.openjfx:javafx-${module}:${javafxVersion}:${platform}"
        }
    }
}

test {
    useJUnitPlatform()
}

application {
    getMainClass().set('avs.simulation.Launcher')
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

shadowJar {
    archiveBaseName.set('TrafficSimulation')
    archiveVersion.set('1.0')
    archiveClassifier.set('')
    manifest {
        attributes 'Main-Class': 'avs.simulation.Launcher'
    }
}